plugins {
    id "com.diffplug.spotless" version "6.11.0"
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

project.version="0.3.5"

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        content {
            includeGroup "io.btrace"
        }
        mavenContent {
            snapshotsOnly()
        }
        // see https://central.sonatype.org/publish/publish-portal-snapshots/#consuming-via-gradle
        url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

spotless {
    // Format Java in all modules
    java {
        // Use an explicit FileTree to ensure excludes are honored
        target fileTree(project.rootDir) {
            include '**/src/**/*.java'
            exclude '**/.history/**'
            exclude '**/.idea/**'
            exclude '**/.vscode/**'
            exclude '**/.gradle/**'
            exclude '**/build/**'
        }
        googleJavaFormat('1.17.0')
        trimTrailingWhitespace()
        endWithNewline()
    }
    // Format Groovy plugin sources
    groovy {
        target fileTree('jafar-gradle-plugin/src') {
            include '**/*.groovy'
            exclude '**/.history/**'
            exclude '**/.idea/**'
            exclude '**/.vscode/**'
            exclude '**/.gradle/**'
            exclude '**/build/**'
        }
        greclipse()
        trimTrailingWhitespace()
        endWithNewline()
    }
    // Format Gradle build scripts
    format 'gradle', {
        target fileTree(project.rootDir) {
            include '*.gradle'
            include '**/*.gradle'
            exclude '**/.history/**'
            exclude '**/.idea/**'
            exclude '**/.vscode/**'
            exclude '**/.gradle/**'
            exclude '**/build/**'
        }
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

nexusPublishing {
    packageGroup = "io.btrace"
    repositories {
        sonatype {
          // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
          // see https://github.com/gradle-nexus/publish-plugin#publishing-to-maven-central-via-sonatype-central
          // Also for official doc
          // staging repo publishing https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
          // snapshot publishing https://central.sonatype.org/publish/publish-portal-snapshots/#publishing-via-other-methods
          nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
          snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))

            username = project.hasProperty("sonatype.user") ? project.property("sonatype.user") : System.getenv("SONATYPE_USERNAME")
            password = project.hasProperty("sonatype.password") ? project.property("sonatype.password") : System.getenv("SONATYPE_PASSWORD")
        }
    }
}

// Ensure formatting is checked during all builds
allprojects {
    tasks.matching { it.name == 'check' }.configureEach {
        dependsOn rootProject.tasks.named('spotlessCheck')
    }
}
