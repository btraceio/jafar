plugins {
    id 'java'
    id 'application'
    // No shadow plugin; define a simple fatJar task compatible with Gradle 9
    id 'io.btrace.jafar-gradle-plugin' version '0.3.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    // Gradle 9+: use Provider API for mainClass
    mainClass.set('io.jafar.demo.Main')
}

tasks.register('explodedDist', Copy) {
    dependsOn installDist
    from(layout.buildDirectory.dir("install/${project.name}")) // Location where the application plugin installs the distribution
    into(layout.buildDirectory.dir("exploded/${project.name}")) // Target directory for the exploded distribution
}

tasks.build {
    dependsOn explodedDist
}

// Create a self-contained fat jar without relying on shadow's application integration
tasks.register('fatJar', Jar) {
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from({ configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } })
    manifest {
        attributes('Main-Class': 'io.jafar.demo.Main')
    }
}

tasks.assemble { dependsOn(tasks.named('fatJar')) }

dependencies {
    implementation 'io.btrace:jafar-parser:0.3.0-SNAPSHOT'
    implementation 'io.btrace:jafar-tools:0.3.0-SNAPSHOT'

    implementation 'it.unimi.dsi:fastutil:8.5.12'
    implementation 'org.jctools:jctools-core:4.0.3'
    implementation 'org.openjdk.jmc:flightrecorder:8.3.1'
}

test {
    useJUnitPlatform()
}

generateJafarTypes {
    overwrite = true
//    inputFile = file('<some_datadog_profile.jfr>')
    eventTypeFilter {
        it == 'jdk.CPULoad' ||
        it == 'jdk.ExecutionSample' ||
        it == 'jdk.GCHeapSummary' ||
        it == 'jdk.ThreadCPULoad' ||
        it == 'jdk.SocketRead' ||
        it == 'jdk.ThreadAllocationStatistics' ||
        it == 'jdk.VirtualThreadPinned' ||
        it.startsWith('datadog.')
    }
    targetPackage = 'io.jafar.demo.types'
}
