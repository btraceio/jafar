plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.vanniktech.maven.publish' version '0.35.0'
}

group = "io.btrace"
version = "0.5.0-SNAPSHOT"

def libraryName = "jafar-gradle-plugin"
def component_version = version

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        content {
            includeGroup "io.btrace"
        }
        mavenContent {
            snapshotsOnly()
        }
        // see https://central.sonatype.org/publish/publish-portal-snapshots/#consuming-via-gradle
        url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation gradleApi() // Provides the Gradle API, including Plugin
    implementation localGroovy()

    implementation "io.btrace:jafar-parser:${component_version}"
    implementation 'org.jctools:jctools-core:4.0.1'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.5'
    implementation 'org.openjdk.jmc:flightrecorder:8.3.1'
}

gradlePlugin {
    plugins {
        create("jafarGradlePlugin") {
            id = "io.btrace.jafar-gradle-plugin"
            implementationClass = "io.jafar.gradle.TypeGeneratorPlugin"
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveBaseName.set(libraryName)
    archiveClassifier.set("sources")
    archiveVersion.set(component_version)
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveBaseName.set(libraryName)
    archiveClassifier.set('javadoc')
    archiveVersion.set(component_version)
    from(layout.buildDirectory.dir("docs/javadoc"))
}

description = 'Gradle plugin for generating JFR type interfaces from recordings'

// Publish to both Maven Central and GitHub Packages (releases only for GitHub Packages)
publishing {
    repositories {
        // Only publish to GitHub Packages for releases (not snapshots)
        if (!version.toString().endsWith("-SNAPSHOT")) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/btraceio/jafar")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

// Configure vanniktech plugin for Maven Central publishing
mavenPublishing {
    // v0.35.0+ automatically uses Central Portal (SonatypeHost enum removed)
    publishToMavenCentral()
    signAllPublications()

    coordinates("io.btrace", "jafar-gradle-plugin", version.toString())

    pom {
        name = "jafar-gradle-plugin"
        description = project.description
        url = 'https://github.com/btraceio/jafar'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'jbachorik'
                name = 'Jaroslav Bachorik'
                email = 'j.bachorik@gmail.com'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/btraceio/jafar.git'
            developerConnection = 'scm:git:ssh://github.com/btraceio/jafar.git'
            url = 'https://github.com/btraceio/jafar'
        }
    }
}
