plugins {
    id 'java-library'
    id 'maven-publish'
}

def libraryName = "jfr-shell-jafar"
// Independent versioning for backend plugins
def component_version = "0.1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

dependencies {
    // Only depends on jfr-shell for the SPI interfaces (compile-only)
    compileOnly project(':jfr-shell')

    // Depends on jafar parser for actual parsing implementation
    implementation project(':parser')

    testImplementation project(':jfr-shell')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
            'Implementation-Title': 'JFR Shell Jafar Backend',
            'Implementation-Version': component_version
        )
    }
}

group = 'io.btrace'
version = component_version
description = 'Jafar parser backend for jfr-shell'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.btrace'
            artifactId = libraryName
            version = component_version

            from components.java

            pom {
                name = 'JFR Shell Jafar Backend'
                description = 'Jafar parser backend for jfr-shell - high-performance JFR parser'
            }
        }
    }
}
