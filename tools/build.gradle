plugins {
    id 'java'
    id 'com.vanniktech.maven.publish'
    id "io.github.goooler.shadow" version "8.1.8"
}

def libraryName = "jafar-tools"
def component_version = project.hasProperty("jafar_version") ? project.jafar_version : rootProject.version

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(':jfr-parser')
}

test {
    useJUnitPlatform()
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveBaseName.set(libraryName)
    archiveClassifier.set("sources")
    archiveVersion.set(component_version)
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveBaseName.set(libraryName)
    archiveClassifier.set('javadoc')
    archiveVersion.set(component_version)
    from(layout.buildDirectory.dir("docs/javadoc"))
}

group = 'io.btrace'
version = component_version
description = 'JFR (Java Flight Recorder) manipulation tools'

// Override artifact name for Maven publishing
mavenPublishing {
    coordinates('io.btrace', 'jafar-tools', version.toString())
}

// Apply code generation plugin only when its tasks are explicitly requested.
if (gradle.startParameter.taskNames.any { it.contains('generateJafarTypes') }) {
    apply plugin: 'io.btrace.jafar-gradle-plugin'
    generateJafarTypes {
        overwrite = false
        targetPackage = 'io.jafar.tools.types'
    }
}
